import static org.gradle.util.CollectionUtils.single

/*
 * Copyright 2008-present MongoDB, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


def javaMainProjects = subprojects.findAll { !['util', 'no-proxy-dep-tests'].contains(it.name) }
task docs(type: Javadoc) {
    destinationDir = new File(rootDir, 'build/docs')
    javaMainProjects.each { proj ->
        proj.tasks.withType(Javadoc).each { javadocTask ->
            source += javadocTask.source
            classpath += javadocTask.classpath
            excludes += javadocTask.excludes
            includes += javadocTask.includes
            dependsOn +=javadocTask.dependsOn
        }
    }
}

def utilProject = project(':util')
allprojects {
    afterEvaluate { project ->
        tasks.withType(Javadoc) {
            dependsOn utilProject.compileJava //We need taglets to be compiled
            exclude "**/internal/**"
            exclude "**/relocated/**"
            options {
                author = true
                version = true
                failOnError = false
                title = "Morphia ${majorVersion} API"
                links 'http://api.mongodb.org/java/3.0/'
                links 'https://docs.oracle.com/javase/9/docs/api/'
                tagletPath single(utilProject.sourceSets.main.output.classesDirs)
                taglets 'ManualTaglet'
                taglets 'ServerReleaseTaglet'
                encoding = 'UTF-8'
                charSet 'UTF-8'
                docEncoding 'UTF-8'
//                addBooleanOption("html4", true)
//                addBooleanOption("-allow-script-in-comments", true)
            }
        }
    }
}
